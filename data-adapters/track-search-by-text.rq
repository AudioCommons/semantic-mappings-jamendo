PREFIX ac: <http://audiocommons.org/ns/audiocommons#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fn: <http://w3id.org/sparql-generate/fn/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX http: <http://www.w3.org/2011/http#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>

GENERATE {
  <http://jamendo.com>
      rdf:type prov:Agent, foaf:Organization ;
      foaf:name "Jamendo" .

  ?searchAction
      a schema:SearchAction ;
      schema:object <http://jamendo.com> ;
      schema:query "$pattern" ;
      schema:result ?audioCollection .

  ?audioCollection
      rdf:type ac:AudioCollection, prov:Entity ;
      prov:wasAttributedTo <http://jamendo.com> ;
      ac:memberNode ?audioCollectionNode ;
      ac:nodeCount ?nodeCount .

  ?audioCollectionNode
      a ac:AudioCollectionNode ;
      ac:nodeIndex ?index ;
      ac:nodeContent ?audioClip .

  ?audioClip
      rdf:type ac:AudioClip ;
      dc:title ?title ;
      cc:license ?license ;
      ac:compiled ?artistURI ;
      ac:available_as ?audioFile ;
      rdf:type prov:Entity ;
      prov:wasAttributedTo <http://jamendo.com> .
  ?audioFile rdf:type ac:AudioFile .
}
SOURCE <https://api.jamendo.com/v3.0/tracks/?client_id=$token&format=json&search=$pattern> AS ?source
ITERATOR iter:JSONElement(?source,"$.results[*]") AS ?resIterator
WHERE {
  {	SELECT ?searchAction ?audioCollection WHERE {
      BIND(BNODE() AS ?searchAction)
      BIND(BNODE() AS ?audioCollection)
    }
  }
  BIND(BNODE() AS ?audioCollectionNode)
  BIND(fn:JSONPath(?resIterator, "element") AS ?res)
  BIND(fn:JSONPath(?resIterator, "position") AS ?indexFromZero)
  BIND(?indexFromZero + 1 AS ?index)

  BIND(IRI(fn:JSONPath(?res, "shareurl")) AS ?audioClip)
  BIND(fn:JSONPath(?res, "name") AS ?title)
  BIND(iri(fn:JSONPath(?res, "license_ccurl")) as ?license)
  BIND(IRI(fn:JSONPath(?res, "audiodownload")) AS ?audioFile)
  BIND(fn:JSONPath(?res, "artist_id") AS ?artist_id)
  BIND(iri(CONCAT("http://www.jamendo.com/artist/", ?artist_id)) as ?artistURI)
}
